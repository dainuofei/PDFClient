plugins {
    id "java"
    id "idea"
    id "org.jetbrains.kotlin.jvm"
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "net.minecraftforge.gradle.forge"
    id "org.spongepowered.mixin"
    id "io.gitlab.arturbosch.detekt"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = "https://repo.spongepowered.org/repository/maven-public/" }
    maven { url = "https://jitpack.io/" }
    maven { url = "https://getfdp.today/repo/" }
}

version = "2.0.0"
group = "org.unlegitmc.fdp"
archivesBaseName = "FDPClient"

sourceCompatibility = targetCompatibility = 1.8
compileJava.options.encoding = "UTF-8"

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = false
    clientJvmArgs += ["-Dfml.coreMods.load=net.ccbluex.liquidbounce.injection.forge.TransformerLoader", "-Xmx1024m -Xms1024m", "-Ddev-mode"]
}

configurations {
    include
    implementation.extendsFrom(include)
}

dependencies {
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:$detekt_version"

    include("org.spongepowered:mixin:0.7.11-SNAPSHOT") {
        transitive = false
        exclude module: "guava"
        exclude module: "commons-io"
        exclude module: "gson"
        exclude module: "launchwrapper"
        exclude module: "log4j-core"
    }

    annotationProcessor("org.spongepowered:mixin:0.7.11-SNAPSHOT")

    include "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    // TODO: switch to MCEF
    include "com.labymedia:ultralight-java-base:0.4.6"
    include "com.labymedia:ultralight-java-databind:0.4.6"
    include "com.github.unlegitmc:fuckpcl:2.0"
    include 'com.jagrosh:DiscordIPC:0.4'
    include("me.liuli:Elixir:1.2.1") {
        exclude module: "kotlin-stdlib"
        exclude module: "authlib"
    }

    include project(":ui")

    include fileTree(include: ["*.jar"], dir: "libs")
}

shadowJar {
    archiveClassifier.set("")
    configurations = [project.configurations.include]
    duplicatesStrategy DuplicatesStrategy.EXCLUDE

    // Ultralight native files are going to be downloaded from our cloud
    exclude "native-binaries/**"

    exclude "LICENSE.txt"

    exclude "com/sun/jna/**"

    exclude "META-INF/maven/**"
    exclude "META-INF/versions/**"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    filesMatching("mcmod.info") {
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    rename "(.+_at.cfg)", "META-INF/\$1"
}

task moveResources {
    doLast {
        ant.move file: "${buildDir}/resources/main",
                todir: "${buildDir}/classes/java"
    }
}

moveResources.dependsOn(processResources)
classes.dependsOn(moveResources)

jar {
    manifest.attributes(
            "FMLCorePlugin": "net.ccbluex.liquidbounce.injection.forge.TransformerLoader",
            "FMLCorePluginContainsFMLMod": true,
            "ForceLoadAsMod": true,
            "MixinConfigs": "mixins.fdpclient.json",
            "ModSide": "CLIENT",
            "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
            "TweakOrder": "0",
            "FMLAT": "fdpclient_at.cfg",
    )

    enabled = false
}

mixin {
    disableRefMapWarning = true
    defaultObfuscationEnv searge
    add sourceSets.main, "mixins.fdpclient.refmap.json"
}

reobf {
    shadowJar {
        mappingType = "SEARGE"
    }
}

detekt {
    toolVersion = "$detekt_version"
    input = files("$projectDir")
    config = files("$project.projectDir/detekt.yml")
//    buildUponDefaultConfig = true
    basePath = projectDir

//    reports {
//        reports {
//            html.enabled = true
//            xml.enabled = true
//            txt.enabled = false
//            sarif.enabled = false
//        }
//    }

    autoCorrect = true
}
tasks.getByPath("detekt").onlyIf { gradle.startParameter.taskNames.any { it.contains("detekt") } }

tasks.reobfJar.dependsOn(shadowJar)